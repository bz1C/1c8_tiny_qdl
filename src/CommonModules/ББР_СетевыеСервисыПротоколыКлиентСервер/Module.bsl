// Библиотека "быстрой" разработки на платформе 1С:Предприятие 8
// Модуль ББР_СервисыИнтернетКлиентСервер. Версия 1.1 от 25.04.2021
// Назначение: Содержит функции для работы с сетевыми сервисами и протоколами
// Зависимости: ББР_ПарсингКлиентСервер.
// Автор: Чернуль Александр Владимирович. E-mail: bzero@yandex.ru
// Лицензия на использование: Freeware.

Функция ВыполнитьHTTPЗапрос(знач URL, ТекстОшибки, знач Пользователь = Неопределено, знач Пароль = Неопределено, знач ИмяМетода = "GET", знач СтрокаТелаЗапроса = "") Экспорт
	
	ТекстОшибки = Неопределено;
	СтруктураURL = ББР_ПарсингКлиентСервер.РаспарситьURL(URL);
	
	Если СтруктураURL = Неопределено Тогда
		ТекстОшибки = НСтр("ru='Некорректный формат URL'");
		Возврат Неопределено; 
	КонецЕсли; 
	
	Если СтруктураURL.Протокол = "HTTPS" Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
			Новый СертификатКлиентаWindows(),
            Новый СертификатыУдостоверяющихЦентровWindows()
		);   
        
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли; 
	
	Попытка
		// Создать HTTP-соединение с сервером localhost.
		HTTPСоединение = Новый HTTPСоединение(СтруктураURL.Сервер, СтруктураURL.Порт, Пользователь, Пароль,,,ЗащищенноеСоединение);
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		// Вывести сообщение об ошибке соединения с сервером.
		Возврат Неопределено;
	КонецПопытки; 
	
	// Создать HTTP-запрос на основе URL.
	Если ТипЗнч(СтрокаТелаЗапроса) = Тип("HTTPЗапрос") Тогда
		HTTPЗапрос = СтрокаТелаЗапроса;
		Если ЗначениеЗаполнено(СтруктураURL.Адрес) и СтруктураURL.Адрес <> "/" Тогда
			HTTPЗапрос.АдресРесурса = СтруктураURL.Адрес;
		КонецЕсли; 
	Иначе
		HTTPЗапрос = Новый HTTPЗапрос(СтруктураURL.Адрес);
		// Установить тело запроса из строки.
		Если ИмяМетода <> "DELETE" И ИмяМетода <> "GET" Тогда
			HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаТелаЗапроса);
		КонецЕсли;
	КонецЕсли; 
	
	
	
	Попытка
		// Получить ответ сервера в виде объекта HTTPОтвет.
		Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, HTTPЗапрос);
		
	Исключение
		// Вывести сообщение об ошибке при получении ответа сервера.
		ТекстОшибки = ОписаниеОшибки();
		Возврат Неопределено;
	КонецПопытки; 
	
	Возврат Ответ; 
	
	// Получить содержимое ответа сервера в виде строки можно используя:
	//Результат = Ответ.ПолучитьТелоКакСтроку();
КонецФункции 
