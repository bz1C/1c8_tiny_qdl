// Библиотека "быстрой" разработки на платформе 1С:Предприятие 8
// Модуль ББР_Сериализация. Версия 1.0 от 20.04.2021
// Назначение: Содержит функции сериализации в строковые данных различных форматов. Работает на сервере.
// Зависимости: Отсутствуют.
// Автор: Чернуль Александр Владимирович. E-mail: bzero@yandex.ru
// Лицензия на использование: Freeware.

#Область СериализуемыеФорматы

Функция СсылкаНаДокументВСтруктуру(знач ИмяТипа, знач Значение, знач РезультатПриОшибкеСериализации = Неопределено, знач СохранятьДанныеОТипе = Неопределено, знач СериализуемыеСвойства = Неопределено) Экспорт 
	ТипЗначения = ТипЗнч(Значение);
	Если не Документы.ТипВсеСсылки().СодержитТип(ТипЗначения) Тогда
		Возврат РезультатПриОшибкеСериализации; 
	КонецЕсли;
	Если ЗначениеЗаполнено(ИмяТипа) и ТипЗначения <> Тип("ДокументСсылка."+ИмяТипа) Тогда
		Возврат РезультатПриОшибкеСериализации; 
	КонецЕсли; 
	
	Если СериализуемыеСвойства = Неопределено Тогда
		СтруктРезультата = Новый Структура("Представление, GUID");
	Иначе
		СтруктРезультата = Новый Структура(СериализуемыеСвойства);
	КонецЕсли;
	
	
	КолСвойств = СтруктРезультата.Количество();
	
	Если СтруктРезультата.Свойство("GUID") Тогда
		КолСвойств = КолСвойств - 1;
	КонецЕсли; 
	
	Если СтруктРезультата.Свойство("Представление") Тогда
		КолСвойств = КолСвойств - 1;
	КонецЕсли;
	
	Если СтруктРезультата.Свойство("Тип") Тогда
		КолСвойств = КолСвойств - 1;
	КонецЕсли; 
	
	Если КолСвойств > 0 Тогда
		ЗаполнитьЗначенияСвойств(СтруктРезультата, Значение);
	КонецЕсли; 
	
	Если СтруктРезультата.Свойство("GUID") Тогда
		Если не ЗначениеЗаполнено(Значение) Тогда
			ИдСтр = "";
		Иначе
			ИдСтр = Строка(Значение.УникальныйИдентификатор());
		КонецЕсли;
		СтруктРезультата.GUID = ИдСтр;
	КонецЕсли; 
	
	Если СтруктРезультата.Свойство("Представление") Тогда
		СтруктРезультата.Представление = Строка(Значение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяТипа) и СохранятьДанныеОТипе = Истина или не ЗначениеЗаполнено(ИмяТипа) и СохранятьДанныеОТипе <> Ложь Тогда
		СтруктРезультата.Вставить("Тип", Значение.Метаданные().ПолноеИмя());
	КонецЕсли;
	
	Возврат СтруктРезультата; 
КонецФункции

#КонецОбласти 