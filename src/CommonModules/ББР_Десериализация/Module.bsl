// Библиотека "быстрой" разработки на платформе 1С:Предприятие 8
// Модуль ББР_Десериализация. Версия 1.0 от 20.04.2021
// Назначение: Содержит функции десериализации строковых данных из различных форматов. Работает на сервере.
// Зависимости: ББР_КоллекцииКлиентСервер, ББР_СтроковыеКлиентСервер.
// Автор: Чернуль Александр Владимирович. E-mail: bzero@yandex.ru
// Лицензия на использование: Freeware.

#Область JSON

Функция СсылкаНаДокументИзСтруктуры(знач ИмяВидаДокумента, Значение) Экспорт
	Если ТипЗнч(Значение) <> Тип("Структура") Тогда
		Если Значение = "" и ЗначениеЗаполнено(ИмяВидаДокумента) Тогда
			Попытка
				Возврат ПредопределенноеЗначение("Документ."+ИмяВидаДокумента+".ПустаяСсылка"); 		
			Исключение
				Возврат СтрШаблон(НСтр("ru='Неверный вид документа ""%1""'"), ИмяВидаДокумента); 
			КонецПопытки; 
		КонецЕсли; 
		Возврат НСтр("ru='Неверный формат значения'"); 
	КонецЕсли;
	
	УИД = ББР_КоллекцииКлиентСервер.СвойствоСтруктуры(Значение, "GUID", Неопределено);
	ТипДокумента = ББР_КоллекцииКлиентСервер.СвойствоСтруктуры(Значение, "Тип", "");
	Представление = ББР_КоллекцииКлиентСервер.СвойствоСтруктуры(Значение, "Представление", "");
		
	Если ЗначениеЗаполнено(ТипДокумента) и не ББР_СтроковыеКлиентСервер.СтрНачинаетсяС_БезУчетаРегистра(ТипДокумента, "Документ.") Тогда
		Значение.Вставить("ТекстОшибки", СтрШаблон(НСтр("ru='Объект ""%1"" не является документом'"), Представление));
		Возврат Значение; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ИмяВидаДокумента) и ЗначениеЗаполнено(ТипДокумента) Тогда
		Если СтрСравнить("Документ."+ИмяВидаДокумента, ТипДокумента) Тогда
			Значение.Вставить("ТекстОшибки", СтрШаблон(
				НСтр("ru='Фактический тип ""%1"" объекта ""%3"" не соответствует ожидаемому ""%2""'"),
				ТипДокумента,
				"Документ."+ИмяВидаДокумента,
				Представление
			));
			Возврат Значение; 			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ТипДокумента) Тогда
		ИмяВидаДокумента = ББР_СтроковыеКлиентСервер.СтрПодстрокаПослеРазделителя(ТипДокумента, ".");
	КонецЕсли; 
	
	Если ПустаяСтрока(ИмяВидаДокумента) Тогда
		Значение.Вставить("ТекстОшибки", СтрШаблон(НСтр("ru='Вид ссылки на документ ""%1"" не задан явно и не указан при сериализации'"), Представление));
		Возврат Значение; 			
	КонецЕсли; 
	
	Попытка
		Менеджер = Документы[ИмяВидаДокумента];
	Исключение
		Значение.Вставить("ТекстОшибки", СтрШаблон(
			НСтр("ru='Указан некорректный вид ""%1"" документа ""%2"", отсутствующий в конфигурации"),
			ИмяВидаДокумента,
			Представление
		));
		Возврат Значение; 			
	КонецПопытки; 
	
	Если не ЗначениеЗаполнено(УИД) Тогда
		//Значение.Вставить("ТекстОшибки", СтрШаблон(НСтр("ru='Не заполнен GUID документа ""%1""'"), Представление));
		Значение.Вставить("Ссылка", Менеджер.ПустаяСсылка());
		Возврат Значение; 
	КонецЕсли; 
	
	Попытка
		УУИД = Новый УникальныйИдентификатор(УИД);
	Исключение
		Значение.Вставить("ТекстОшибки", СтрШаблон(
			НСтр("ru='Некорректный формат уникального идентификатора ""%1"" ссылки ""%2""'"),
			УИД,
			ББР_КоллекцииКлиентСервер.СвойствоСтруктуры(Значение, "Представление", "")
		));
		Возврат Значение; 			
	КонецПопытки; 
	
	Ссыль = Менеджер.ПолучитьСсылку(УУИД);
	
	Значение.Вставить("Ссылка", Ссыль);
	
	Возврат Значение; 
КонецФункции

#КонецОбласти 