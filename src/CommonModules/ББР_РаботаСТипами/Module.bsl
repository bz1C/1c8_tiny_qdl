// Библиотека "быстрой" разработки на платформе 1С:Предприятие 8
// Модуль ББР_РаботаСТипами. Версия 1.0 от 24.10.2018
// Назначение: Содержит функции для работы с типами данных
// Автор: Чернуль Александр Владимирович. E-mail: bzero@yandex.ru
// Лицензия на использование: Freeware.

Функция ПолучитьЭлементарныйТипИзСтроки(знач ПолноеИмяТипа) Экспорт 
	ПолноеИмяТипа = НРег(ПолноеИмяТипа);
	Если ПолноеИмяТипа="н" или ПолноеИмяТипа="неопределено" Тогда
		Возврат Тип("Неопределено");
	ИначеЕсли ПолноеИмяТипа="0" или ПолноеИмяТипа="null" Тогда
		Возврат Тип("NULL");
	ИначеЕсли ПолноеИмяТипа="д" или ПолноеИмяТипа="дата" Тогда
		Возврат Тип("Дата");
	ИначеЕсли ПолноеИмяТипа="л" или ПолноеИмяТипа="булево" Тогда
		Возврат Тип("Булево");
	ИначеЕсли ПолноеИмяТипа="c" или ПолноеИмяТипа="строка" Тогда
		Возврат Тип("Строка");
	ИначеЕсли ПолноеИмяТипа="ч" или ПолноеИмяТипа="число" Тогда
		Возврат Тип("Число");
	КонецЕсли; 
	
	Возврат Неопределено; 
КонецФункции


// Функция - Преобразует строку с описанием типов в объект ОписаниеТипов
//
// Параметры:
//  СтрОписаниеТипов			 - Строка - Содержит один или несколько типов, разделенных символом РазделительТипов. Каждый тип может быть
//											любым типом, который можно указать в конструкторе объекта ОписаниеТипов() в строке.
//  РазделительТипов			 - Строка - Символ или символы, каждый из которых является разделителем различных типов.
//  ВызыватьИсключениеПриОшибке	 - Булево - Если Истина, то в случае ошибки вызывается исключение. Если Ложь, ф-ия игнорирует некритические ошибки.
// 
// Возвращаемое значение:
//  ОписаниеТипов, Неопределено - Возвращает описание типов, перечисленных в строке через разделитель. Если ни одного типа нет, возвращает Неопределено.
//
Функция ОписаниеТиповИзСтроки(знач СтрОписаниеТипов, знач РазделительТипов=",", ВызыватьИсключениеПриОшибке = Истина) Экспорт 
	МасОписаниеТипов = СтрРазделить(СтрОписаниеТипов, РазделительТипов, Ложь);
	МасТипов = Новый Массив;
	Результат = Новый ОписаниеТипов;
	Для каждого СтрОписаниеТипа Из МасОписаниеТипов Цикл
		СтрОписаниеТипаСокр = СокрЛП(СтрОписаниеТипа);
		ЭлТип = ПолучитьЭлементарныйТипИзСтроки(СтрОписаниеТипаСокр);
		Если ЭлТип <> Неопределено Тогда
			МасТипов.Добавить(ЭлТип);
		Иначе
			Попытка
				ЭлТип = Тип(СтрОписаниеТипаСокр);
			Исключение
				Если ВызыватьИсключениеПриОшибке = Истина Тогда
					ВызватьИсключение "[ОписаниеТиповИзСтроки()#1]. "+СтрШаблон(НСтр("ru='В параметре ""СтрОписаниеТипов"" указано неверное имя типа ""%1"".'"), СтрОписаниеТипаСокр);
				Иначе
					Продолжить;
				КонецЕсли; 
			КонецПопытки; 
			
			МасТипов.Добавить(ЭлТип);
		КонецЕсли; 
	КонецЦикла; 
	
	Если МасТипов.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Новый ОписаниеТипов(МасТипов);
	КонецЕсли; 
КонецФункции

Функция ЭтоОбъект(Объект) Экспорт
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	ЗаписатьXML(ЗаписьXML, Объект);
	СериализованныйОбъект = ЗаписьXML.Закрыть();
	ПозЗакрывающийТег = СтрНайти(СериализованныйОбъект, ">");
	Если ПозЗакрывающийТег > 0 Тогда
		СтрокаСТипом = Лев(СериализованныйОбъект, ПозЗакрывающийТег);
	Иначе
		СтрокаСТипом = СериализованныйОбъект;
	КонецЕсли; 
	Возврат СтрНайти(СтрокаСТипом,"Object.") <> 0;
КонецФункции

// Возвращает пустое значение с тем же типом, что и у значения параметра 
//
//ПАРАМЕТРЫ
//	Значение:ПроизвольныйТип.
//ВОЗВРАЩАЕТ
//	ПроизвольныйТип. ПустоеЗначение произвольного типа.
Функция ПолучитьПустоеЗначениеТипаПоЗначению(Значение) Экспорт
	ТипЗн=ТипЗнч(Значение);
	МассивТипа=Новый Массив();
	МассивТипа.Добавить(ТипЗн);
	ОписаниеТипа=Новый ОписаниеТипов(МассивТипа);
	Возврат ОписаниеТипа.ПривестиЗначение();
КонецФункции // ПолучитьПустоеЗначениеТипа(ТекСтр.ДокументПартии)
