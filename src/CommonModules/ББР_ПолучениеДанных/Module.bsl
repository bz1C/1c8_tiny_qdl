// Библиотека "быстрой" разработки на платформе 1С:Предприятие 8
// Модуль требует платформу 1С:Предприятие версии не ниже 8.3.21 (КатегорияОшибки.ОшибкаКонфигурации)
// Модуль ББР_ПолучениеДанных. Версия 1.4 от 08.05.2023
// Назначение: Содержит функции, получающие данные из базы.
// Зависимости: ББР_КоллекцииКлиентСервер, ББР_РаботаСМетаданными. 
// Вторичные зависимости: ББР_СтроковыеКлиентСервер.
// Автор: Чернуль Александр Владимирович. E-mail: bzero@yandex.ru
// Лицензия на использование: Freeware.


Функция ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов) Экспорт
	  МасСсылок = Новый Массив(1);
	  МасСсылок[0] = Ссылка;
	  
	  РезультатСоотв = ЗначенияРеквизитовОбъектов(МасСсылок, ИменаРеквизитов);
	  РезультатСтрукт = РезультатСоотв[Ссылка];
	  РезультатСоотв = Неопределено;
	  Возврат РезультатСтрукт; 
КонецФункции

Функция ЗначенияРеквизитовОбъектов(МассивСсылокИлиСоответствие, ИменаРеквизитов) Экспорт
	Результат = Новый Соответствие;
	
	Если МассивСсылокИлиСоответствие.Количество() = 0 Тогда
		Возврат Результат; 
	КонецЕсли; 
	
	Если ТипЗнч(МассивСсылокИлиСоответствие) = Тип("Соответствие") Тогда
		МассивСсылок = ББР_КоллекцииКлиентСервер.СоответствиеВМассив(МассивСсылокИлиСоответствие);
	Иначе
		МассивСсылок = МассивСсылокИлиСоответствие;
	КонецЕсли; 	
	
	МасИменаРеквизитов = СтрРазделить(ИменаРеквизитов, ", ", Ложь);
	Если МасИменаРеквизитов.Количество() = 0 Тогда
		ВызватьИсключение(
			НСтр("ru='Отсутствуют реквизиты для получения'"),
			КатегорияОшибки.ОшибкаКонфигурации,
			"ББР.ЗначенияРеквизитовОбъектов.ОтсутствуютРеквизиты"
		);
	КонецЕсли; 
		
	Выборка = СоставитьИВыполнитьЗапросПолучениеРеквизитов(МасИменаРеквизитов, МассивСсылок);
	
	ИменаПолей = СтрЗаменить(ИменаРеквизитов, ".", "");
	Пока Выборка.Следующий() Цикл
		ЗначенияРеквизитов = Новый Структура(ИменаПолей);
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
		Результат.Вставить(Выборка.__Ссылка, ЗначенияРеквизитов); 
	КонецЦикла; 
	
	Возврат Результат; 
КонецФункции

Функция ЗначенияРеквизитаОбъектов(МассивСсылокИлиСоответствие, ИмяРеквизита) Экспорт 
	Результат = Новый Соответствие;
	
	Если МассивСсылокИлиСоответствие.Количество() = 0 Тогда
		Возврат Результат; 
	КонецЕсли; 
	
	Если ТипЗнч(МассивСсылокИлиСоответствие) = Тип("Соответствие") Тогда
		МассивСсылок = ББР_КоллекцииКлиентСервер.СоответствиеВМассив(МассивСсылокИлиСоответствие);
	Иначе
		МассивСсылок = МассивСсылокИлиСоответствие;
	КонецЕсли; 	
	
	Если СтрНайти(ИмяРеквизита,",") > 0 или СтрНайти(ИмяРеквизита," ") > 0 Тогда
		ВызватьИсключение(
			НСтр("ru='Неверное имя реквизита или указано несколько реквизитов, хотя разрешено указать только один'"),
			КатегорияОшибки.ОшибкаКонфигурации,
			"ББР.ЗначенияРеквизитаОбъектов.НеверныйРеквизит"
		);
	КонецЕсли; 
	
	МасИменаРеквизитов = Новый Массив;
	МасИменаРеквизитов.Добавить(ИмяРеквизита);
	Выборка = СоставитьИВыполнитьЗапросПолучениеРеквизитов(МасИменаРеквизитов, МассивСсылок);
	
	ИмяПоля = СтрЗаменить(ИмяРеквизита, ".", "");
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.__Ссылка, Выборка[ИмяПоля]); 
	КонецЦикла; 
	
	Возврат Результат; 
КонецФункции
 
Функция СоставитьИВыполнитьЗапросПолучениеРеквизитов(Знач МасИменаРеквизитов, Знач МассивСсылок)
	
	Перем Выборка, Запрос, ИмяТаблицы, ПутьКРеквизиту, СинонимРеквизита, ТекстЗапроса;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ";
	
	Для каждого ПутьКРеквизиту Из МасИменаРеквизитов Цикл
		СинонимРеквизита = СтрЗаменить(ПутьКРеквизиту, ".", "");
		ТекстЗапроса = ТекстЗапроса + Символы.ПС+"	Т."+ПутьКРеквизиту+" КАК "+СинонимРеквизита+",";
	КонецЦикла; 
	
	ИмяТаблицы = МассивСсылок[0].Метаданные().ПолноеИмя();
	
	ТекстЗапроса = ТекстЗапроса +"
	|	Т.Ссылка КАК __Ссылка
	|ИЗ
	|	"+ИмяТаблицы+" КАК Т
	|ГДЕ
	|	Т.Ссылка В(&МассивСсылок)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;

КонецФункции


// Функция - Проверяет, является ли ссылка "битой", т.е. отсутствует ли в БД объект с указанной ссылкой.
//
// Параметры:
//  СсылкаНаОбъект	 - Ссылка - Ссылка, проверяемая на "битость"
// 
// Возвращаемое значение:
//  Булево - Истина, если ссылка является "битой"
//
Функция БитаяСсылка(СсылкаНаОбъект) Экспорт
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Т.Ссылка
	|ИЗ
	|	Справочник.Банки КАК Т
	|ГДЕ
	|	Т.Ссылка = &Ссылка";
	Запрос.Текст=СтрЗаменить(Запрос.Текст,"Справочник.Банки КАК Т",СсылкаНаОбъект.Метаданные().ПолноеИмя()+" КАК Т");
	Запрос.УстановитьПараметр("Ссылка",СсылкаНаОбъект);
	Возврат Запрос.Выполнить().Пустой();
КонецФункции

// Функция - Получает значения реквизита объектов, которые могут храниться как в одной, так и в разных таблицах БД.
//
//  Зависимости: ББР_РаботаСМетаданными.ЕстьРеквизитОбъекта().
//  Вторичные зависимости: ББР_СтроковыеКлиентСервер.ПроверитьПолучитьЧислоИзСтроки()
// Параметры:
//  МассивСоответствиеСсылок - Массив, Соответствие - Ссылки, значение реквизита которых нужно получить. Если передается соответствие, то ссылки должны являться ключами соответствия.
//  ИменаРеквизитов			 - Строка - Имяена реквизитов, значения которых нужно получить. Допускается обращение "через точку".
//  ТолькоРазрешенные		 - Булево - Истина, если нужно получить только разрешенные данные.
// 
// Возвращаемое значение:
//  Соответствие - Ключ - ссылка на объект, Значение - структура, ключами которой являются имена реквизитов, а значениями - значение данного реквизита. 
//		Если ключа с переданным значением ссылки нет, то ссылка является "битой", пустой, либо на неё нет прав, если ТолькоРазрешенные=Истина.
//
Функция ЗначенияРеквизитовОбъектовРазныхТаблиц(МассивСоответствиеСсылок, знач ИменаРеквизитов, ТолькоРазрешенные = Ложь) Экспорт
	МасИменаРеквизитов = СтрРазделить(ИменаРеквизитов, ",", Ложь);
	Если МасИменаРеквизитов.Количество() = 0 Тогда
		ВызватьИсключение(
			НСтр("ru='Отсутствуют реквизиты для получения'"),
			КатегорияОшибки.ОшибкаКонфигурации,
			"ББР.ЗначенияРеквизитаОбъектовРазныхТаблиц.ОтсутствуютРеквизиты"
		);
	КонецЕсли; 
	
	СоотвРеквизитыПсевдонимРазбивка = ПолучитьСоответствиеИмяПоляМасПутиКРеквизиту(ИменаРеквизитов);

	СоотвСсылкиПоТипам = ЗначенияРеквизитовОбъектовРазныхТаблиц_РаспределитьСсылкиПоТипам(МассивСоответствиеСсылок);
		
	Результат = Новый Соответствие;
	
	Если СоотвСсылкиПоТипам.Количество() = 0 Тогда
		Возврат Результат; 
	КонецЕсли;
		
	Пока СоотвСсылкиПоТипам.Количество() > 0 Цикл
		Запрос = ЗначенияРеквизитовОбъектовРазныхТаблиц_СформироватьОбъединенныйЗапрос(СоотвСсылкиПоТипам, СоотвРеквизитыПсевдонимРазбивка, ТолькоРазрешенные);
		
		//Цикл реализован для того, чтобы учесть ограничение MS SQL Server на 255 таблиц в запросе
		//@skip-warning
		Выборка = Запрос.Выполнить().Выбрать();
		
		ИменаПолей = СтрЗаменить(ИменаРеквизитов, ".", "");
		Пока Выборка.Следующий() Цикл
			ЗначенияРеквизитов = Новый Структура(ИменаПолей);
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			Результат.Вставить(Выборка.__Ссылка, ЗначенияРеквизитов); 
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат; 
КонецФункции

Функция ПолучитьСоответствиеИмяПоляМасПутиКРеквизиту(Знач ИменаРеквизитов)
	Перем ПсевдонимПоля;
	Перем МасРеквизиты;
	Перем ПутьКРеквизиту;
	Перем Элемент;
	Перем ЭлементМас;
	Перем инд;
	СоотвРеквизитыПсевдонимРазбивка = Новый Соответствие;
	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		 МасРеквизиты = СтрРазделить(ИменаРеквизитов, ",", Ложь);
		 ИменаРеквизитов = Новый Соответствие;
		 Для Каждого ПутьКРеквизиту Из МасРеквизиты Цикл
		 	ПсевдонимПоля = СтрЗаменить(ПутьКРеквизиту,".","");
		 	ПсевдонимПоля = СтрЗаменить(ПсевдонимПоля," ","");		 	
		 	ИменаРеквизитов.Вставить(ПсевдонимПоля, ПутьКРеквизиту);
		 КонецЦикла;
	КонецЕсли;
	Если ТипЗнч(ИменаРеквизитов) = Тип("Соответствие") Тогда
		СоотвРеквизитыПсевдонимРазбивка = ИменаРеквизитов;
		Для Каждого Элемент Из ИменаРеквизитов Цикл
			Если ТипЗнч(Элемент.Значение) = Тип("Строка") Тогда
				 МасРеквизиты = СтрРазделить(Элемент.Значение, ".");
				 инд = 0;
				 Для Каждого ЭлементМас Из МасРеквизиты Цикл
				 	МасРеквизиты[инд] = СокрЛП(ЭлементМас); 
				 	инд = инд + 1;  
				 КонецЦикла;
			Иначе
				МасРеквизиты = Элемент.Значение;
			КонецЕсли;
			СоотвРеквизитыПсевдонимРазбивка.Вставить(Элемент.Ключ, МасРеквизиты); 
		КонецЦикла;
	КонецЕсли;
	Возврат СоотвРеквизитыПсевдонимРазбивка;
КонецФункции

// Функция - Получает значения реквизита объектов, которые могут храниться как в одной, так и в разных таблицах БД.
//
//  Зависимости: ББР_РаботаСМетаданными.ЕстьРеквизитОбъекта().
//  Вторичные зависимости: ББР_СтроковыеКлиентСервер.ПроверитьПолучитьЧислоИзСтроки()
//  Параметры:
//  МассивСоответствиеСсылок - Массив, Соответствие - Ссылки, значение реквизита которых нужно получить. Если передается соответствие, то ссылки должны являться ключами соответствия.
//  ИмяРеквизита			 - Строка - Имя реквизита, значение которого нужно получить. Допускается обращение "через точку".
//  ТолькоРазрешенные		 - Булево - Истина, если нужно получить только разрешенные данные.
// 
// Возвращаемое значение:
//  Соответствие - Ключ - ссылка на объект, Значение  значение реквизита ИмяРеквизита. 
//		Если ключа с переданным значением ссылки нет, то ссылка является "битой", пустой, либо на неё нет прав, если ТолькоРазрешенные=Истина.
//
Функция ЗначенияРеквизитаОбъектовРазныхТаблиц(МассивСоответствиеСсылок, ИмяРеквизита, ТолькоРазрешенные = Ложь) Экспорт
	СоотвСсылкиПоТипам = ЗначенияРеквизитовОбъектовРазныхТаблиц_РаспределитьСсылкиПоТипам(МассивСоответствиеСсылок);
		
	Результат = Новый Соответствие;
	
	Если СоотвСсылкиПоТипам.Количество() = 0 Тогда
		Возврат Результат; 
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяРеквизита) Тогда
		ВызватьИсключение(
			НСтр("ru='Отсутствуют реквизиты для получения'"),
			КатегорияОшибки.ОшибкаКонфигурации,
			"ББР.ЗначенияРеквизитаОбъектовРазныхТаблиц.ОтсутствуетИмяКолонки"
		);
	КонецЕсли; 
	
	СоотвРеквизитыПсевдонимРазбивка = ПолучитьСоответствиеИмяПоляМасПутиКРеквизиту(ИмяРеквизита);
	СинонимРеквизита = Неопределено;
	Для Каждого Элемент Из СоотвРеквизитыПсевдонимРазбивка Цикл
		СинонимРеквизита = Элемент.Ключ;
		Прервать;
	КонецЦикла;
		
	Пока СоотвСсылкиПоТипам.Количество() > 0 Цикл
		Запрос = ЗначенияРеквизитовОбъектовРазныхТаблиц_СформироватьОбъединенныйЗапрос(СоотвСсылкиПоТипам, СоотвРеквизитыПсевдонимРазбивка, ТолькоРазрешенные);
		
		//Цикл реализован для того, чтобы учесть ограничение MS SQL Server на 255 таблиц в запросе
		//@skip-warning
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Результат.Вставить(Выборка.__Ссылка, Выборка[СинонимРеквизита]); 
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат Результат; 
КонецФункции

Функция ЗначенияРеквизитовОбъектовРазныхТаблиц_РаспределитьСсылкиПоТипам(Знач МассивСоответствиеСсылок)
	
	Перем КлючЗначение, МассивСсылок, эл;
	СоотвСсылкиПоТипам = Новый Соответствие;
	
	Если ТипЗнч(МассивСоответствиеСсылок) = Тип("Массив") Тогда
		Для каждого эл Из МассивСоответствиеСсылок Цикл
			ЗначенияРеквизитовОбъектовРазныхТаблиц_ДобавитьСсылкуВСоответствиеПоТипам(СоотвСсылкиПоТипам, эл);
		КонецЦикла;
	Иначе
		Для каждого КлючЗначение Из МассивСоответствиеСсылок Цикл
			ЗначенияРеквизитовОбъектовРазныхТаблиц_ДобавитьСсылкуВСоответствиеПоТипам(СоотвСсылкиПоТипам, КлючЗначение.Ключ);
		КонецЦикла;
	КонецЕсли;
	Возврат СоотвСсылкиПоТипам; 
КонецФункции

// СоотвСсылкиПоТипам: Соответствие
//	Тип значения ссылки => Соответствие:
//		Ссылка => Неопределено
Процедура ЗначенияРеквизитовОбъектовРазныхТаблиц_ДобавитьСсылкуВСоответствиеПоТипам(СоотвСсылкиПоТипам, Ссылка)
	Если не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат; 
	КонецЕсли; 
	ТипСсылки = ТипЗнч(Ссылка);
	Если СоотвСсылкиПоТипам.Получить(ТипСсылки) = Неопределено Тогда
		СоотвСсылокТипа = Новый Соответствие;
		СоотвСсылкиПоТипам.Вставить(ТипСсылки, СоотвСсылокТипа);
	Иначе
		СоотвСсылокТипа = СоотвСсылкиПоТипам[ТипСсылки];
	КонецЕсли; 
	СоотвСсылокТипа.Вставить(Ссылка, Неопределено);
КонецПроцедуры


//  Зависимости: ББР_РаботаСМетаданными.ЕстьРеквизитОбъекта().
//  Вторичные зависимости: ББР_СтроковыеКлиентСервер.ПроверитьПолучитьЧислоИзСтроки()
Функция ЗначенияРеквизитовОбъектовРазныхТаблиц_СформироватьОбъединенныйЗапрос(СоотвСсылкиПоТипам, СоотвРеквизитыПсевдонимРазбивка, ТолькоРазрешенные)
	
	Перем Запрос, ИмяТаблицы, МассивСсылок, НомерТаблицы, НомерТаблицыСтр, СсылкаКлючЗначение, СсылкиПоТипу, ТекстЗапроса;
	
	НомерТаблицы = 1;
	МаксимальноеКоличествоТаблицВЗапросе = 255; // ограничение MS SQL Server
	ОбработанныеТипы = Новый Массив(МаксимальноеКоличествоТаблицВЗапросе);
	Запрос = Новый Запрос;
	
	Если ТолькоРазрешенные = Истина Тогда
		МодификаторыОбъединенногоЗапроса = " РАЗРЕШЕННЫЕ";
	Иначе	
		МодификаторыОбъединенногоЗапроса = "";
	КонецЕсли; 
	
	Для каждого СсылкиПоТипу Из СоотвСсылкиПоТипам Цикл
		Если НомерТаблицы = 1 Тогда
			МодификаторыЗапроса = МодификаторыОбъединенногоЗапроса;
		ИначеЕсли НомерТаблицы > МаксимальноеКоличествоТаблицВЗапросе Тогда
			Прервать;
		Иначе
			МодификаторыЗапроса = "";
		КонецЕсли; 

		МассивСсылок = Новый Массив;
		Для каждого СсылкаКлючЗначение Из СсылкиПоТипу.Значение Цикл
			МассивСсылок.Добавить(СсылкаКлючЗначение.Ключ);
		КонецЦикла; 
		мдСсылки = МассивСсылок[0].Метаданные();
		
		ТекстПолейДляПолучения = "";		
		Для каждого Реквизит Из СоотвРеквизитыПсевдонимРазбивка Цикл		
			Если Реквизит.Значение.Количество() = 0 Тогда
				ВызватьИсключение(
					НСтр("ru='Некорректный путь к реквизиту'"),
					КатегорияОшибки.ОшибкаКонфигурации,
					"ББР.ЗначенияРеквизитаОбъектовРазныхТаблиц.НеверныйРеквизит"
				);
			Иначе
				Реквизит1Уровня = Реквизит.Значение[0];
			КонецЕсли;
		
			СинонимРеквизита = Реквизит.Ключ;
			ПутьКРеквизиту = СтрСоединить(Реквизит.Значение, ".");
			
			Если не ББР_РаботаСМетаданными.ЕстьРеквизитОбъекта(мдСсылки, Реквизит1Уровня) Тогда
				ТекстПолейДляПолучения = ТекстПолейДляПолучения + Символы.ПС+"	NULL КАК "+СинонимРеквизита+",";
			Иначе
				ТекстПолейДляПолучения = ТекстПолейДляПолучения + Символы.ПС+"	Т."+ПутьКРеквизиту+" КАК "+СинонимРеквизита+",";
			КонецЕсли;
		КонецЦикла;
		 
		ИмяТаблицы = мдСсылки.ПолноеИмя();
		НомерТаблицыСтр = Формат(НомерТаблицы, "ЧН=; ЧГ="); 
		ТекстЗапроса = "ВЫБРАТЬ"+МодификаторыЗапроса+ТекстПолейДляПолучения+"
		|	Т.Ссылка КАК __Ссылка
		|ИЗ
		|	"+ИмяТаблицы+" КАК Т
		|ГДЕ
		|	Т.Ссылка В(&МассивСсылок"+НомерТаблицыСтр+")";
		
		
		Запрос.УстановитьПараметр("МассивСсылок"+НомерТаблицыСтр, МассивСсылок);
		Если НомерТаблицы=1 Тогда
			Запрос.Текст = ТекстЗапроса;
		Иначе
			Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|"+ТекстЗапроса;
		КонецЕсли; 
		ОбработанныеТипы[НомерТаблицы-1] = СсылкиПоТипу.Ключ; 
		НомерТаблицы = НомерТаблицы+1;
	КонецЦикла;
	
	Если НомерТаблицы > МаксимальноеКоличествоТаблицВЗапросе Тогда
		Для каждого КлючДляУдаления Из ОбработанныеТипы Цикл
			СоотвСсылкиПоТипам.Удалить(КлючДляУдаления);
		КонецЦикла; 
	Иначе
		СоотвСсылкиПоТипам.Очистить();
	КонецЕсли; 
	
	Возврат Запрос;
	
КонецФункции
