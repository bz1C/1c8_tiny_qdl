
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СКД = ПолучитьМакет("МакетСКД");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СКД, Настройки, ДанныеРасшифровки, , Тип("ГенераторМакетаКомпоновкиДанных"));
	
	Организация = ПолучитьЗначенияПараметра(Настройки, "Организация");
	Если Организация = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Не выбрана организация.'");
	КонецЕсли;
	
	ДатаСтандартнаяДатаНачала = ПолучитьЗначенияПараметра(Настройки, "Дата");
	Если ДатаСтандартнаяДатаНачала = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Не указана дата формирования.'");
	КонецЕсли;
	
	ДатаРеестра = ДатаСтандартнаяДатаНачала.Дата;
	
	тзДвижения = ПолучитьТЗДвижений(Организация, ДатаРеестра);
	
	тзОборотыПоСчетам = тзДвижения.Скопировать(, "БанковскийСчет, Организация, СуммаРуб, СуммаПлатежа, ВидДвижения");
	тзОборотыПоСчетам.Свернуть("БанковскийСчет, Организация, ВидДвижения", "СуммаРуб, СуммаПлатежа");
	Для каждого стр Из тзОборотыПоСчетам Цикл
		Если стр.ВидДвижения = Перечисления.ВидыДвиженийПриходРасход.Расход Тогда
			стр.СуммаПлатежа = -стр.СуммаПлатежа;
			стр.СуммаРуб = -стр.СуммаРуб;
		КонецЕсли; 
	КонецЦикла; 
	тзОборотыПоСчетам.Свернуть("БанковскийСчет, Организация", "СуммаРуб, СуммаПлатежа");
	
	тзОстатки = ПолучитьТЗОстатковНаНачало(Организация, ДатаРеестра, тзОборотыПоСчетам);
	
	ВнешниеНаборыДанных = Новый Структура("Остатки, Движения", тзОстатки, тзДвижения);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТЗДвижений(Организация, ДатаРеестра)
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ЗаявкиВРеестрахПлатежей.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|	ЗаявкиВРеестрахПлатежей.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|	ЗаявкиВРеестрахПлатежей.РеестрПлатежей,
	|	РазмещениеЗаявок.БанковскийСчетКасса,
	|	РазмещениеЗаявок.СуммаВзаиморасчетов,
	|	РазмещениеЗаявок.ПриходРасход
	|ПОМЕСТИТЬ ВТ_Платежи
	|ИЗ
	|	РегистрСведений.ЗаявкиВРеестрахПлатежей КАК ЗаявкиВРеестрахПлатежей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеЗаявок КАК РазмещениеЗаявок
	|		ПО ЗаявкиВРеестрахПлатежей.ЗаявкаНаОперацию = РазмещениеЗаявок.ЗаявкаНаОперацию
	|			И ЗаявкиВРеестрахПлатежей.ИдентификаторПозиции = РазмещениеЗаявок.ИдентификаторПозиции
	|ГДЕ
	|	ЗаявкиВРеестрахПлатежей.РеестрПлатежей.ДатаОплаты = &ДатаРеестра
	|	И ЗаявкиВРеестрахПлатежей.ЗаявкаНаОперацию.Организация В ИЕРАРХИИ(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаОперацию,
	|	ИдентификаторПозиции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазмещениеЗаявок.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|	РазмещениеЗаявок.БанковскийСчетКасса,
	|	РазмещениеЗаявок.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|	РазмещениеЗаявок.СуммаВзаиморасчетов,
	|	РазмещениеЗаявок.ПриходРасход,
	|	ВЫБОР
	|		КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|				И РазмещениеЗаявок.ЗаявкаНаОперацию.ТипОперацииБюджетирование = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийБюджетирование.ВнутриГрупповоеПеремещение)
	|			ТОГДА РазмещениеЗаявок.ЗаявкаНаОперацию.ОрганизацияПолучатель
	|		ИНАЧЕ РазмещениеЗаявок.ЗаявкаНаОперацию.Организация
	|	КОНЕЦ КАК Организация
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	РегистрСведений.РазмещениеЗаявок КАК РазмещениеЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИС_ДопХарОбъектовЗначения КАК ИС_ДопХарОбъектовЗначения
	|		ПО РазмещениеЗаявок.ЗаявкаНаОперацию = ИС_ДопХарОбъектовЗначения.Объект
	|ГДЕ
	|	РазмещениеЗаявок.ДатаИсполнения = &ДатаРеестра
	|	И РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|	И ИС_ДопХарОбъектовЗначения.Характеристика = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ИС_ДопХарактеристики.СостояниеПроцесса)
	|	И ИС_ДопХарОбъектовЗначения.Значение В
	|			(ВЫБРАТЬ
	|				ИС_СостоянияЗаявкиНаОперацию.Ссылка
	|			ИЗ
	|				Справочник.ИС_СостоянияЗаявкиНаОперацию КАК ИС_СостоянияЗаявкиНаОперацию
	|			ГДЕ
	|				ИС_СостоянияЗаявкиНаОперацию.РежимПК = ЗНАЧЕНИЕ(Перечисление.ИС_РежимыРаботыПлатежногоКалендаря.Исполнение))
	|	И ВЫБОР
	|			КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|					И РазмещениеЗаявок.ЗаявкаНаОперацию.ТипОперацииБюджетирование = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийБюджетирование.ВнутриГрупповоеПеремещение)
	|				ТОГДА РазмещениеЗаявок.ЗаявкаНаОперацию.ОрганизацияПолучатель
	|			ИНАЧЕ РазмещениеЗаявок.ЗаявкаНаОперацию.Организация
	|		КОНЕЦ В ИЕРАРХИИ (&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаОперацию,
	|	ИдентификаторПозиции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикиСНомеромЦФУ.НомерАналитикиЦФУ,
	|	АналитикиСНомеромЦФУ.ГруппаАналитик КАК ГруппаАналитик
	|ПОМЕСТИТЬ ВТ_НомераАналитикЦФУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ГруппыАналитикСтатьи.ВидАналитики1 = &ВидСубконтоЦФО
	|				ТОГДА 1
	|			КОГДА ГруппыАналитикСтатьи.ВидАналитики2 = &ВидСубконтоЦФО
	|				ТОГДА 2
	|			КОГДА ГруппыАналитикСтатьи.ВидАналитики3 = &ВидСубконтоЦФО
	|				ТОГДА 3
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НомерАналитикиЦФУ,
	|		ГруппыАналитикСтатьи.Ссылка КАК ГруппаАналитик
	|	ИЗ
	|		Справочник.ГруппыАналитикСтатьи КАК ГруппыАналитикСтатьи) КАК АналитикиСНомеромЦФУ
	|ГДЕ
	|	АналитикиСНомеромЦФУ.НомерАналитикиЦФУ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналитик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛимитыРазвернуто.Организация,
	|	ЛимитыРазвернуто.Валюта,
	|	ЛимитыРазвернуто.Проект,
	|	ЛимитыРазвернуто.СтатьяДДС,
	|	ЛимитыРазвернуто.ЦФУ,
	|	СУММА(ЛимитыРазвернуто.СуммаБезКорректировки) КАК СуммаБезКорректировки,
	|	СУММА(ЛимитыРазвернуто.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_Лимиты
	|ИЗ
	|	(ВЫБРАТЬ
	|		БДДСОстатки.Организация КАК Организация,
	|		БДДСОстатки.Валюта КАК Валюта,
	|		БДДСОстатки.Проект КАК Проект,
	|		БДДСОстатки.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
	|		ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 1
	|					ТОГДА БДДСОстатки.Аналитика1
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 2
	|					ТОГДА БДДСОстатки.Аналитика2
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 3
	|					ТОГДА БДДСОстатки.Аналитика3
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			КОНЕЦ КАК Справочник.Организации) КАК ЦФУ,
	|		СУММА(БДДСОстатки.СуммаОстаток) КАК СуммаБезКорректировки,
	|		БДДСОстатки.СуммаОстаток КАК Сумма
	|	ИЗ
	|		РегистрНакопления.БюджетДвиженияДенежныхСредств.Остатки(
	|				,
	|				Организация В ИЕРАРХИИ (&Организация)
	|					И ПериодОтчета = &МесяцОтчета
	|					И Сценарий = &СценарийЛимитДДС) КАК БДДСОстатки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДДС
	|			ПО БДДСОстатки.СтатьяДвиженияДенежныхСредств = СтатьиДДС.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НомераАналитикЦФУ КАК ВТ_НомераАналитикЦФУ
	|			ПО (СтатьиДДС.ГруппаРаскрытия = ВТ_НомераАналитикЦФУ.ГруппаАналитик)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		БДДСОстатки.Организация,
	|		БДДСОстатки.Валюта,
	|		БДДСОстатки.Проект,
	|		БДДСОстатки.СтатьяДвиженияДенежныхСредств,
	|		ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 1
	|					ТОГДА БДДСОстатки.Аналитика1
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 2
	|					ТОГДА БДДСОстатки.Аналитика2
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 3
	|					ТОГДА БДДСОстатки.Аналитика3
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			КОНЕЦ КАК Справочник.Организации),
	|		БДДСОстатки.СуммаОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БДДС.Организация,
	|		БДДС.Валюта,
	|		БДДС.Проект,
	|		БДДС.СтатьяДвиженияДенежныхСредств,
	|		ВЫБОР
	|			КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 1
	|				ТОГДА БДДС.Аналитика1
	|			КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 2
	|				ТОГДА БДДС.Аналитика2
	|			КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 3
	|				ТОГДА БДДС.Аналитика3
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		КОНЕЦ,
	|		-БДДС.Сумма,
	|		0
	|	ИЗ
	|		РегистрНакопления.БюджетДвиженияДенежныхСредств КАК БДДС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДДС
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НомераАналитикЦФУ КАК ВТ_НомераАналитикЦФУ
	|				ПО СтатьиДДС.ГруппаРаскрытия = ВТ_НомераАналитикЦФУ.ГруппаАналитик
	|			ПО БДДС.СтатьяДвиженияДенежныхСредств = СтатьиДДС.Ссылка
	|	ГДЕ
	|		БДДС.Регистратор ССЫЛКА Документ.ПерераспределениеСтатейБюджетов
	|		И БДДС.Активность = ИСТИНА
	|		И БДДС.Сценарий = &СценарийЛимитДДС
	|		И БДДС.Организация В ИЕРАРХИИ(&Организация)
	|		И БДДС.ПериодОтчета = &МесяцОтчета) КАК ЛимитыРазвернуто
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛимитыРазвернуто.ЦФУ,
	|	ЛимитыРазвернуто.Валюта,
	|	ЛимитыРазвернуто.Проект,
	|	ЛимитыРазвернуто.СтатьяДДС,
	|	ЛимитыРазвернуто.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатБезПредставления.Заявка,
	|	РезультатБезПредставления.ИдентификаторПозиции,
	|	РезультатБезПредставления.РеестрПлатежей,
	|	РезультатБезПредставления.БанковскийСчет,
	|	РезультатБезПредставления.СуммаВзаиморасчетов,
	|	РезультатБезПредставления.Проект,
	|	РезультатБезПредставления.Организация,
	|	РезультатБезПредставления.НазначениеПлатежа,
	|	РезультатБезПредставления.ПлатежВУсловныхЕдиницах,
	|	РезультатБезПредставления.ВалютаДокумента,
	|	РезультатБезПредставления.ВалютаОплаты,
	|	РезультатБезПредставления.СуммаПлатежа,
	|	РезультатБезПредставления.СуммаРуб,
	|	РезультатБезПредставления.Контрагент,
	|	РезультатБезПредставления.ВидДвижения,
	|	РезультатБезПредставления.Договор,
	|	РезультатБезПредставления.РеестрПлатежей.Представление,
	|	РезультатБезПредставления.Проект.Представление,
	|	РезультатБезПредставления.Контрагент.Представление,
	|	РезультатБезПредставления.Договор.Представление,
	|	0 КАК ДЗ,
	|	0 КАК КЗ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПлатежнаяПозицияСДополнением.ЗаявкаНаОперацию КАК Заявка,
	|		ПлатежнаяПозицияСДополнением.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|		ПлатежнаяПозицияСДополнением.РеестрПлатежей КАК РеестрПлатежей,
	|		ПлатежнаяПозицияСДополнением.БанковскийСчетКасса КАК БанковскийСчет,
	|		ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|		ПлатежнаяПозицияСДополнением.Проект КАК Проект,
	|		ПлатежнаяПозицияСДополнением.Организация КАК Организация,
	|		ПлатежнаяПозицияСДополнением.НазначениеПлатежа КАК НазначениеПлатежа,
	|		ПлатежнаяПозицияСДополнением.ПлатежВУсловныхЕдиницах КАК ПлатежВУсловныхЕдиницах,
	|		ПлатежнаяПозицияСДополнением.ВалютаДокумента КАК ВалютаДокумента,
	|		ПлатежнаяПозицияСДополнением.ВалютаОплаты КАК ВалютаОплаты,
	|		ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов * ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсВалютыВзаиморасчетовНаДату.Курс, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсВалютыВзаиморасчетовНаДату.Кратность, 0) = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов * КурсВалютыВзаиморасчетовНаДату.Курс / КурсВалютыВзаиморасчетовНаДату.Кратность
	|		КОНЕЦ / ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсВалютыПлатежаНаДату.Курс, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсВалютыПлатежаНаДату.Кратность, 0) = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов * КурсВалютыПлатежаНаДату.Курс / КурсВалютыПлатежаНаДату.Кратность
	|		КОНЕЦ КАК СуммаПлатежа,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсВалютыВзаиморасчетовНаДату.Курс, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсВалютыВзаиморасчетовНаДату.Кратность, 0) = 0
	|				ТОГДА ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов
	|			ИНАЧЕ ПлатежнаяПозицияСДополнением.СуммаВзаиморасчетов * КурсВалютыВзаиморасчетовНаДату.Курс / КурсВалютыВзаиморасчетовНаДату.Кратность
	|		КОНЕЦ КАК СуммаРуб,
	|		ПлатежнаяПозицияСДополнением.ЗаявкаНаОперацию.Контрагент КАК Контрагент,
	|		ПлатежнаяПозицияСДополнением.ПриходРасход КАК ВидДвижения,
	|		ПлатежнаяПозицияСДополнением.ЗаявкаНаОперацию.ДоговорКонтрагента КАК Договор
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ВТ_Платежи.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|			ВТ_Платежи.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|			ВТ_Платежи.РеестрПлатежей КАК РеестрПлатежей,
	|			ВТ_Платежи.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|			ВТ_Платежи.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|			Заявка.Проект КАК Проект,
	|			Заявка.Организация КАК Организация,
	|			Заявка.НазначениеПлатежа КАК НазначениеПлатежа,
	|			Заявка.ВалютаОплаты КАК ВалютаОплаты,
	|			Заявка.ПлатежВУсловныхЕдиницах КАК ПлатежВУсловныхЕдиницах,
	|			Заявка.ВалютаДокумента КАК ВалютаДокумента,
	|			Заявка.СуммаДокумента КАК СуммаДокумента,
	|			ВТ_Платежи.ПриходРасход КАК ПриходРасход
	|		ИЗ
	|			ВТ_Платежи КАК ВТ_Платежи
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОперацию КАК Заявка
	|				ПО ВТ_Платежи.ЗаявкаНаОперацию = Заявка.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ВТ_Поступления.ЗаявкаНаОперацию,
	|			ВТ_Поступления.ИдентификаторПозиции,
	|			NULL,
	|			ВТ_Поступления.БанковскийСчетКасса,
	|			ВТ_Поступления.СуммаВзаиморасчетов,
	|			Заявка.Проект,
	|			ВТ_Поступления.Организация,
	|			Заявка.НазначениеПлатежа,
	|			Заявка.ВалютаОплаты,
	|			Заявка.ПлатежВУсловныхЕдиницах,
	|			Заявка.ВалютаДокумента,
	|			Заявка.СуммаДокумента,
	|			ВТ_Поступления.ПриходРасход
	|		ИЗ
	|			ВТ_Поступления КАК ВТ_Поступления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОперацию КАК Заявка
	|				ПО ВТ_Поступления.ЗаявкаНаОперацию = Заявка.Ссылка) КАК ПлатежнаяПозицияСДополнением
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРеестра, ) КАК КурсВалютыПлатежаНаДату
	|			ПО ПлатежнаяПозицияСДополнением.ВалютаОплаты = КурсВалютыПлатежаНаДату.Валюта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРеестра, ) КАК КурсВалютыВзаиморасчетовНаДату
	|			ПО ПлатежнаяПозицияСДополнением.ВалютаДокумента = КурсВалютыВзаиморасчетовНаДату.Валюта) КАК РезультатБезПредставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПоАналитикам.Ссылка КАК Заявка,
	|	РасшифровкаПоАналитикам.Сумма КАК СуммаПоСтроке,
	|	РасшифровкаПоАналитикам.СтатьяДДС,
	|	РасшифровкаПоАналитикам.ЦФУ,
	|	РасшифровкаПоАналитикам.ЦФУ.Представление,
	|	ВТ_Лимиты.Сумма КАК ОстатокЛимитаКонецМесяца,
	|	ВТ_Лимиты.СуммаБезКорректировки КАК ОстатокЛимитаКонецМесяцаБезКорректировок,
	|	РасшифровкаПоАналитикам.СтатьяДДС.Представление
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 1
	|					ТОГДА ДО.АналитикаБДДС1
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 2
	|					ТОГДА ДО.АналитикаБДДС2
	|				КОГДА ВТ_НомераАналитикЦФУ.НомерАналитикиЦФУ = 3
	|					ТОГДА ДО.АналитикаБДДС3
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			КОНЕЦ КАК Справочник.Организации) КАК ЦФУ,
	|		ДО.Сумма КАК Сумма,
	|		ДО.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
	|		ЗаявкаНаОперацию.Организация КАК Организация,
	|		ЗаявкаНаОперацию.ВалютаДокумента КАК ВалютаДокумента,
	|		ЗаявкаНаОперацию.Проект КАК Проект,
	|		ЗаявкаНаОперацию.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ЗаявкаНаОперацию.ДвиженияОперации КАК ДО
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию
	|			ПО ДО.Ссылка = ЗаявкаНаОперацию.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДвиженияДенежныхСредств
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НомераАналитикЦФУ КАК ВТ_НомераАналитикЦФУ
	|				ПО СтатьиДвиженияДенежныхСредств.ГруппаРаскрытия = ВТ_НомераАналитикЦФУ.ГруппаАналитик
	|			ПО ДО.СтатьяДвиженияДенежныхСредств = СтатьиДвиженияДенежныхСредств.Ссылка
	|	ГДЕ
	|		ДО.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_Платежи.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию
	|				ИЗ
	|					ВТ_Платежи КАК ВТ_Платежи
	|			
	|				ОБЪЕДИНИТЬ
	|			
	|				ВЫБРАТЬ
	|					ВТ_Поступления.ЗаявкаНаОперацию
	|				ИЗ
	|					ВТ_Поступления КАК ВТ_Поступления)) КАК РасшифровкаПоАналитикам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Лимиты КАК ВТ_Лимиты
	|		ПО РасшифровкаПоАналитикам.Организация = ВТ_Лимиты.Организация
	|			И РасшифровкаПоАналитикам.Проект = ВТ_Лимиты.Проект
	|			И РасшифровкаПоАналитикам.ЦФУ = ВТ_Лимиты.ЦФУ
	|			И РасшифровкаПоАналитикам.СтатьяДДС = ВТ_Лимиты.СтатьяДДС
	|			И РасшифровкаПоАналитикам.ВалютаДокумента = ВТ_Лимиты.Валюта";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРеестра", ДатаРеестра);
	Запрос.УстановитьПараметр("ВидСубконтоЦФО", ПланыВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникЦФО);
	
	Запрос.УстановитьПараметр("МесяцОтчета", Справочники.Периоды.ПолучитьПроизвольныйПериод(
		НачалоМесяца(ДатаРеестра), 
		НачалоДня(КонецМесяца(ДатаРеестра)),
		Истина));
		
	Запрос.УстановитьПараметр("СценарийЛимитДДС", Справочники.Сценарии.План);
	
	
	Результат=Запрос.ВыполнитьПакет();
	
	ТЗЛимиты = Результат[Результат.ВГраница()].Выгрузить();
	ТЗДвижения = Результат[Результат.ВГраница()-1].Выгрузить();
	
	ТЗРезультатРаспределение = ББР_Распределение.РаспределитьПропорциональноБазеПоИзмерениям(
		ТЗДвижения,
		ТЗЛимиты,
		"Заявка",
		"СуммаПоСтроке",
		"СуммаПлатежа, СуммаВзаиморасчетов, СуммаРуб",
		"СтатьяДДС, ЦФУ, ОстатокЛимитаКонецМесяца, ОстатокЛимитаКонецМесяцаБезКорректировок, СтатьяДДСПредставление, ЦФУПредставление",
		2);
				
	Возврат ТЗРезультатРаспределение;
	
КонецФункции

&НаСервере
Функция ПолучитьЗначенияПараметра(Настройки, ИмяПараметра)
	
	НайденныйПараметрКД = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	
	Если НайденныйПараметрКД = Неопределено Тогда
		Возврат Неопределено; 
	КонецЕсли;
	
	Если НайденныйПараметрКД.Использование = Ложь Тогда
		Возврат Неопределено; 
	КонецЕсли; 
	
	Возврат НайденныйПараметрКД.Значение;
	
КонецФункции

&НаСервере
Функция ПолучитьТЗОстатковНаНачало(Организация, ДатаРеестра, ОборотыЗаДень)
		
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ОборотыЗаДень.БанковскийСчет,
	|	ОборотыЗаДень.Организация,
	|	ОборотыЗаДень.СуммаПлатежа,
	|	ОборотыЗаДень.СуммаРуб
	|ПОМЕСТИТЬ ВТ_ОборотыЗаДень
	|ИЗ
	|	&ОборотыЗаДень КАК ОборотыЗаДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОстаткиНаНачалоИКонец.БанковскийСчет.ПлатежныйИнструмент = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)
	|			ТОГДА ""Касса""
	|		ИНАЧЕ ОстаткиНаНачалоИКонец.БанковскийСчет.ВидСчета
	|	КОНЕЦ КАК ВидСчета,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.НомерСчета КАК НомерСчета,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.Представление КАК БанковскийСчетПредставление,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ВалютаДенежныхСредств.Представление КАК ВалютаПредставление,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ДатаЗакрытия КАК ДатаЗакрытия,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет,
	|	ОстаткиНаНачалоИКонец.Организация,
	|	СУММА(ОстаткиНаНачалоИКонец.ОстатокВалНач) КАК ОстатокВалНач,
	|	СУММА(ОстаткиНаНачалоИКонец.ОстатокРубНач) КАК ОстатокРубНач,
	|	СУММА(ОстаткиНаНачалоИКонец.ОстатокРубКон) КАК ОстатокРубКон,
	|	СУММА(ОстаткиНаНачалоИКонец.ОстатокВалКон) КАК ОстатокВалКон
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДСОстаткиНач.БанковскийСчетКасса КАК БанковскийСчет,
	|		ДСОстаткиНач.Организация КАК Организация,
	|		ДСОстаткиНач.СуммаОстаток КАК ОстатокВалНач,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсыВалютНаДатуРеестра.Кратность, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсыВалютНаДатуРеестра.Курс, 0) = 0
	|				ТОГДА ДСОстаткиНач.СуммаОстаток
	|			ИНАЧЕ ДСОстаткиНач.СуммаОстаток * КурсыВалютНаДатуРеестра.Курс / КурсыВалютНаДатуРеестра.Кратность
	|		КОНЕЦ КАК ОстатокРубНач,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсыВалютНаДатуРеестра.Кратность, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсыВалютНаДатуРеестра.Курс, 0) = 0
	|				ТОГДА ДСОстаткиНач.СуммаОстаток
	|			ИНАЧЕ ДСОстаткиНач.СуммаОстаток * КурсыВалютНаДатуРеестра.Курс / КурсыВалютНаДатуРеестра.Кратность
	|		КОНЕЦ КАК ОстатокРубКон,
	|		ДСОстаткиНач.СуммаОстаток КАК ОстатокВалКон
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредства.Остатки(&ДатаРеестра, Организация В ИЕРАРХИИ (&Организация)) КАК ДСОстаткиНач
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРеестра, ) КАК КурсыВалютНаДатуРеестра
	|			ПО ДСОстаткиНач.БанковскийСчетКасса.ВалютаДенежныхСредств = КурсыВалютНаДатуРеестра.Валюта
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ОборотыЗаДень.БанковскийСчет,
	|		ВТ_ОборотыЗаДень.Организация,
	|		0,
	|		0,
	|		ВТ_ОборотыЗаДень.СуммаРуб,
	|		ВТ_ОборотыЗаДень.СуммаПлатежа
	|	ИЗ
	|		ВТ_ОборотыЗаДень КАК ВТ_ОборотыЗаДень
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаявкиРасходПросроченоНеВыполнено.БанковскийСчет,
	|		ЗаявкиРасходПросроченоНеВыполнено.Организация,
	|		ЗаявкиРасходПросроченоНеВыполнено.Остаток,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсыВалютНаДатуРеестра.Кратность, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсыВалютНаДатуРеестра.Курс, 0) = 0
	|				ТОГДА ЗаявкиРасходПросроченоНеВыполнено.Остаток
	|			ИНАЧЕ ЗаявкиРасходПросроченоНеВыполнено.Остаток * КурсыВалютНаДатуРеестра.Курс / КурсыВалютНаДатуРеестра.Кратность
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(КурсыВалютНаДатуРеестра.Кратность, 0) = 0
	|					ИЛИ ЕСТЬNULL(КурсыВалютНаДатуРеестра.Курс, 0) = 0
	|				ТОГДА ЗаявкиРасходПросроченоНеВыполнено.Остаток
	|			ИНАЧЕ ЗаявкиРасходПросроченоНеВыполнено.Остаток * КурсыВалютНаДатуРеестра.Курс / КурсыВалютНаДатуРеестра.Кратность
	|		КОНЕЦ,
	|		ЗаявкиРасходПросроченоНеВыполнено.Остаток
	|	ИЗ
	|		(ВЫБРАТЬ
	|			РазмещениеЗаявок.БанковскийСчетКасса КАК БанковскийСчет,
	|			СУММА(ВЫБОР
	|					КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|						ТОГДА РазмещениеЗаявок.Сумма
	|					ИНАЧЕ -РазмещениеЗаявок.Сумма
	|				КОНЕЦ) КАК Остаток,
	|			ВЫБОР
	|				КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|						И ДокументЗаявка.ТипОперацииБюджетирование = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийБюджетирование.ВнутриГрупповоеПеремещение)
	|					ТОГДА ДокументЗаявка.ОрганизацияПолучатель
	|				ИНАЧЕ ДокументЗаявка.Организация
	|			КОНЕЦ КАК Организация
	|		ИЗ
	|			Документ.ЗаявкаНаОперацию КАК ДокументЗаявка
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрСостоянийОбъектов.СрезПоследних КАК РегистрСостоянийОбъектовСрезПоследних
	|				ПО ДокументЗаявка.Ссылка = РегистрСостоянийОбъектовСрезПоследних.Объект
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеЗаявок КАК РазмещениеЗаявок
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИсполненияДокументовПланирования.СрезПоследних КАК СостоянияИсполненияДокументовПланированияСрезПоследних
	|					ПО РазмещениеЗаявок.ИдентификаторПозиции = СостоянияИсполненияДокументовПланированияСрезПоследних.ИдентификаторПозиции
	|						И РазмещениеЗаявок.ЗаявкаНаОперацию = СостоянияИсполненияДокументовПланированияСрезПоследних.ДокументПланирования
	|				ПО ДокументЗаявка.Ссылка = РазмещениеЗаявок.ЗаявкаНаОперацию
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИС_ДопХарОбъектовЗначения КАК ИС_ДопХарОбъектовЗначения
	|				ПО ДокументЗаявка.Ссылка = ИС_ДопХарОбъектовЗначения.Объект
	|					И (ИС_ДопХарОбъектовЗначения.Характеристика = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ИС_ДопХарактеристики.СостояниеПроцесса))
	|		ГДЕ
	|			ДокументЗаявка.Проведен
	|			И ДокументЗаявка.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход)
	|			И РазмещениеЗаявок.ДатаИсполнения >= ДОБАВИТЬКДАТЕ(&ДатаРеестра, ДЕНЬ, -&ГлубинаОтображенияПросроченныхПлатежей)
	|			И РазмещениеЗаявок.ДатаИсполнения < &ДатаРеестра
	|			И РегистрСостоянийОбъектовСрезПоследних.СостояниеОбъекта В (ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.Утверждена), ЗНАЧЕНИЕ(Перечисление.СостоянияСогласования.НаУтверждении))
	|			И НЕ ЕСТЬNULL(СостоянияИсполненияДокументовПланированияСрезПоследних.СостояниеИсполнения, ЗНАЧЕНИЕ(Перечисление.СостоянияИсполненияЗаявки.НеОбработана)) В (ЗНАЧЕНИЕ(Перечисление.СостоянияИсполненияЗаявки.Отложена), ЗНАЧЕНИЕ(Перечисление.СостоянияИсполненияЗаявки.Исполнена))
	|			И (ИС_ДопХарОбъектовЗначения.Значение ЕСТЬ NULL
	|					ИЛИ ИС_ДопХарОбъектовЗначения.Значение В
	|						(ВЫБРАТЬ
	|							Т.Ссылка
	|						ИЗ
	|							Справочник.ИС_СостоянияЗаявкиНаОперацию КАК Т
	|						ГДЕ
	|							Т.РежимПК = ЗНАЧЕНИЕ(Перечисление.ИС_РежимыРаботыПлатежногоКалендаря.Исполнение)))
	|			И ВЫБОР
	|					КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|							И ДокументЗаявка.ТипОперацииБюджетирование = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийБюджетирование.ВнутриГрупповоеПеремещение)
	|						ТОГДА ДокументЗаявка.ОрганизацияПолучатель
	|					ИНАЧЕ ДокументЗаявка.Организация
	|				КОНЕЦ В ИЕРАРХИИ(&Организация)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РазмещениеЗаявок.БанковскийСчетКасса,
	|			ВЫБОР
	|				КОГДА РазмещениеЗаявок.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|						И ДокументЗаявка.ТипОперацииБюджетирование = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийБюджетирование.ВнутриГрупповоеПеремещение)
	|					ТОГДА ДокументЗаявка.ОрганизацияПолучатель
	|				ИНАЧЕ ДокументЗаявка.Организация
	|			КОНЕЦ) КАК ЗаявкиРасходПросроченоНеВыполнено
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаРеестра, ) КАК КурсыВалютНаДатуРеестра
	|			ПО ЗаявкиРасходПросроченоНеВыполнено.БанковскийСчет.ВалютаДенежныхСредств = КурсыВалютНаДатуРеестра.Валюта) КАК ОстаткиНаНачалоИКонец
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНаНачалоИКонец.Организация,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет,
	|	ВЫБОР
	|		КОГДА ОстаткиНаНачалоИКонец.БанковскийСчет.ПлатежныйИнструмент = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)
	|			ТОГДА ""Касса""
	|		ИНАЧЕ ОстаткиНаНачалоИКонец.БанковскийСчет.ВидСчета
	|	КОНЕЦ,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.НомерСчета,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.Представление,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ВалютаДенежныхСредств,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ВалютаДенежныхСредств.Представление,
	|	ОстаткиНаНачалоИКонец.БанковскийСчет.ДатаЗакрытия";
	
	Запрос.УстановитьПараметр("ДатаРеестра", ДатаРеестра);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОборотыЗаДень", ОборотыЗаДень);
	Запрос.УстановитьПараметр("ГлубинаОтображенияПросроченныхПлатежей", Константы.ГлубинаОтображенияПросроченныхПлатежей.Получить());
	
	тзОстатки=Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	Возврат тзОстатки; 
КонецФункции